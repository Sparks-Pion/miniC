## 任务401：LLVM IR初识

### 任务描述

我们的Mini-C语言经过语法分析后，结构信息已经保存在AST中。这次实验是语义分析，用中间语言描述语言的语义，包括语义检查等。我们希望将AST翻译为LLVM IR，完成之后，再借助LLVM的强大功能，操纵IR代码，进行优化、目标代码生成等。
为了完成语义翻译，需要同学们先了解LLVM IR的大致语法，掌握其格式，为AST的语义翻译进行准备。
请查阅资料，完成以下题目，掌握LLVM IR基本使用。

### LLVM的简单认识

LLVM是一个托管和开发一组紧密结合的底层工具链组件(例如，汇编器、编译器、调试器等)的综合项目，其设计目的是与Unix系统上通常使用的现有工具兼容。“LLVM”是（Low Level Virtual Machine）的缩略词。虽然LLVM提供了一些独特的功能，并以其一些重量级的工具而闻名(如，Clang compiler，它提供了许多优于GCC编译器的优点)。

传统静态编译器(像大多数C编译器一样)最流行的设计是三个阶段的设计，其主要组件是前端、优化器和后端。前端分析源代码，检查错误，并构建特定于语言的抽象语法树(抽象语法树，AST)来表示输入代码。AST可选地转换为新的表示以进行优化，优化器和后端在代码上运行。如下图所示。

![[Three Major Components of a Three-Phase Compiler]](https://www.aosabook.org/images/llvm/SimpleCompiler.png)

在基于LLVM的编译器中，前端负责解析、验证和诊断输入代码中的错误，然后将解析的代码转换为LLVM IR。这个IR可以通过一系列分析和优化来改进代码，然后将其发送到代码生成器中以生成本机代码。这是上述三阶段设计的一个非常直接的实现，但是这个简单的描述掩盖了LLVM体系结构从LLVM IR派生出来的一些功能和灵活性。如下图所示。

![[LLVM's Implementation of the Three-Phase Design]](https://www.aosabook.org/images/llvm/LLVMCompiler1.png)

*这里，将主要在LLVM IR指令、LLVM IR API两方面介绍，主要覆盖从minic到LLVM IR生成过程中所需要的IR相关内容，而关于LLVM IR的完整、深入的知识，请感兴趣的同学，参考官方资料。*
前面语法分析中，AST建立，实际已经参考了llvm的一个语言实例：
https://llvm.org/docs/tutorial/MyFirstLanguageFrontend/index.html

### LLVM IR

LLVM设计中最重要的方面是LLVM中间表示(Intermediate Representation, IR)，这是它在编译器中用来表示代码的形式。LLVM IR的设计目的是承载在编译器的优化器部分中可以找到的中层分析和转换。它的设计有许多特定的目标，包括支持轻量级运行时优化、跨功能/过程间优化、全局程序分析和积极的重构转换等。然而它最重要的方面是，它本身被定义为具有定义良好的语义的第一类语言。

下面，以简单的`C`代码`a.c`（主要完成`函数inc（）`）为例，对比其`LLVM IR`的代码`a.ll`。
可以通过命令`clang -emit-llvm -S a.c`得到。生成的IR代码`a.ll`可以使用`lli`直接运行。

可以看到IR代码中前四行描述了架构等信息，后面中的`align 4`表示对齐四字节，也是架构相关；下一关中，IR代码生成中没有设置架构，不会出现该字段。详细了解，可以阅读后面的参考资料。

在LLVM语言中，使用基于`SSA(Static Single Value，静态单一赋值)`表达方式，提供类型安全的、底层操作、灵活、强大的表达所有高层语言的能力。
LLVM代码，有三种形式：`内存中`编译器IR；`磁盘中`字节码（适合快速被JIT编译器装载）；易于`人阅读`的汇编语言代码。这三种形式是等价的。
通过提供类型信息，LLVM可以完成目标优化，例如：通过指针分析，能找出C语言某个自动变量在当前函数外未被使用，从而将其变为简单的`SSA`值。
LLVM文档中主要包括

- 高层结构(`High Level Structure`)、
- 类型系统(`Type System`)、
- 常量(`Constants`)、其他值、元数据、
- 指令手册（`Instruction Reference`)等。

我们根据实验需要，挑选部分内容做介绍。
内存操作相关的，`alloca`、`load`、`store` 指令。

1. 每一个变量都需要使用alloca申请空间；例如，为变量`%1`申请空间：

    ```python
    %1 = alloca i32, align 4  
    ```

2. 使用store将值写到内存中；例如

    ```python
    store i32 %5, i32* %1, align 4 
    ```

3. 使用load来读取内存（变量在使用前需要加载，并且加载到的'临时变量'尽量使用一次，避免出现与内存值不同）；例如：

    ```python
    %3 = load i32, i32* %1, align 4 
    ```

    下一次再使用`%1`变量，仍需先完成加载；

    例：

    ```python
    %ptr = alloca i32                               ; yields i32*:ptr
    store i32 3, i32* %ptr                          ; yields void
    %val = load i32, i32* %ptr                      ; yields i32:val = i32 3 
    ```

详细内容可以参考[Identifiers](https://llvm.org/docs/LangRef.html#id1437)(<https://llvm.org/docs/LangRef.html#identifiers>)。

```c
#include<stdio.h>

int k;
int inc(){
    int a=1;
    int b=3;
    a = a + b;
    //分支结构
    if(a>0){
        a = a+'0';
    }
    putchar(a);
    return 0;
}
```

```python
; ModuleID = 'example.c'
source_filename = "example.c"
target datalayout = "e-m:e-p270:32:32-p271:32:32-p272:64:64-i64:64-f80:128-n8:16:32:64-S128"
target triple = "x86_64-pc-linux-gnu"

@k = common dso_local global i32 0, align 4

; Function Attrs: noinline nounwind optnone uwtable
define dso_local i32 @inc() #0 {
  %1 = alloca i32, align 4  ;a
  %2 = alloca i32, align 4  ;b
  store i32 1, i32* %1, align 4   ;a=1
  store i32 3, i32* %2, align 4   ;b=3
  %3 = load i32, i32* %1, align 4   
  %4 = load i32, i32* %2, align 4
  %5 = add nsw i32 %3, %4       ;%5=a+b
  store i32 %5, i32* %1, align 4   ;a=%5
  %6 = load i32, i32* %1, align 4  ;%6=a
  %7 = icmp sgt i32 %6, 0       ;%7=(%6>0)
  br i1 %7, label %8, label %11   ;if(%7)goto label %8 else goto label%11

8:                                                ; preds = %0
  %9 = load i32, i32* %1, align 4
  %10 = add nsw i32 %9, 48
  store i32 %10, i32* %1, align 4
  br label %11

11:                                               ; preds = %8, %0
  %12 = load i32, i32* %1, align 4
  %13 = call i32 @putchar(i32 %12)
  ret i32 0
}

declare dso_local i32 @putchar(i32) #1

attributes #0 = { noinline nounwind optnone uwtable "correctly-rounded-divide-sqrt-fp-math"="false" "disable-tail-calls"="false" "frame-pointer"="all" "less-precise-fpmad"="false" "min-legal-vector-width"="0" "no-infs-fp-math"="false" "no-jump-tables"="false" "no-nans-fp-math"="false" "no-signed-zeros-fp-math"="false" "no-trapping-math"="false" "stack-protector-buffer-size"="8" "target-cpu"="x86-64" "target-features"="+cx8,+fxsr,+mmx,+sse,+sse2,+x87" "unsafe-fp-math"="false" "use-soft-float"="false" }
attributes #1 = { "correctly-rounded-divide-sqrt-fp-math"="false" "disable-tail-calls"="false" "frame-pointer"="all" "less-precise-fpmad"="false" "no-infs-fp-math"="false" "no-nans-fp-math"="false" "no-signed-zeros-fp-math"="false" "no-trapping-math"="false" "stack-protector-buffer-size"="8" "target-cpu"="x86-64" "target-features"="+cx8,+fxsr,+mmx,+sse,+sse2,+x87" "unsafe-fp-math"="false" "use-soft-float"="false" }

!llvm.module.flags = !{!0}
!llvm.ident = !{!1}

!0 = !{i32 1, !"wchar_size", i32 4}
!1 = !{!"clang version 10.0.0-4ubuntu1 "}

```

#### 常量[Constants](https://llvm.org/docs/LangRef.html#constants)

LLVM中，常量包括简单常量（布尔常量、整型常量、...);复杂常量（结构常量、数组常量、...)。
具体与类型相关（`Type`）

上面IR代码例子中，可以看到常量`i32 1`、`i32 3`；
浮点常量等的内容可以参考[Constants](https://llvm.org/docs/LangRef.html#id1502)[¶](https://llvm.org/docs/LangRef.html#constants)。

#### 变量[Identifiers](https://llvm.org/docs/LangRef.html#identifiers)

LLVM变量包括两种基本类型：全局与局部。
全局变量[Global-Variables](https://llvm.org/docs/LangRef.html#global-variables)(functions,global variables)用`@`开头；
局部变量Local（register names,types)用`%`开头。
1.命名的变量`%foo`,`@DivisionByZero`；
2.未命名的变量，`%12`,`@2`,`%44`；

语言中保留关键字与其他语言类似。不同操作码有保留字（`add`,`bitcast`,`ret`,...)，基本类型（`void``,i32`,...)。这些保留字，不会与变量名冲突，因为没有用前缀`%`或`@`开头。
变量id的命名规则与高级语言基本相同（Regular experiision: `[%@][-a-zA-Z$._][-a-zA-Z$._0-9]*`)；使用时，需要符合SSA的策略，即静态单赋值。

前面样例代码中，`%2 = alloca i32`表示，申请了一个长为i32的无名字变量%2。
变量类型有int、float、double等，如上所示i32表示长32比特的int类型整数。浮点数的内容可参考[Single Value Types](https://llvm.org/docs/LangRef.html#id1488)[¶](https://llvm.org/docs/LangRef.html#single-value-types)。

#### 表达式计算[Binary-operations](https://llvm.org/docs/LangRef.html#binary-operations)

加减乘除等计算都有llvm IR指令与之对应，并且不同类型的变量计算有与之对应的指令。

如`%5 = add nsw i32 %3, %4`为一个加法运算。
其中的`nsw`表示`no signed warp`，当存在时，出现了有符号的溢出，将会真正影响计算值，在后续中无需考虑。浮点数加法对应的指令为`fadd`，减法请参考[Binary Operations](https://llvm.org/docs/LangRef.html#id1716)[¶](https://llvm.org/docs/LangRef.html#binary-operations)。

乘法运算与加法类似，对应的指令为`mul`、`fmul`。
例如：整型变量%x乘8：
简单写法：

```python
%result = mul i32 %x,8 
```

强度削弱后：

```python
%result = shl i32 %x, 3 
```

用加法完成：

```python
%0 = add i32 %X, %X           ; yields i32:%0
%1 = add i32 %0, %0           ; yields i32:%1
%result = add i32 %1, %1 
```

最后这种写法中，展现了LLVM以下词法特性：

1. 注释以`;`开始，直至行结束。注释指明了该指令产生的值的`类型`:`名字`（`i32:%0`)。
2. 无命名临时变量：当计算结果未赋值给某个有命名的值时，创建无命名临时变量（`%0`、`%1`）。
3. 无命名临时变量用数字编号（每个过程里面自动累加，从`0`开始）。基本块、未命名函数参数都属于该数字编号的范围。例如，如果入口基本块(`the entry basic block`)没有给出标号名字，而函数参数都已经命名，则入口基本块会自动获得数字`0`为编号。
与汇编指令类似，除法指令有无符号除法`udiv`和有符号除法`sdiv`，对应的浮点数除法为`fdiv`。

其他二元计算基本都与汇编类似，都可以参考[Binary Operations](https://llvm.org/docs/LangRef.html#id1716)[¶](https://llvm.org/docs/LangRef.html#binary-operations)。

#### 函数[Functions](https://llvm.org/docs/LangRef.html#functions)

如上函数定义对应的代码为`define dso_local i32 @inc() #0 {`，其中的`dso_local`和`#0`为函数的描述和修饰字段，`#0`与最后的`attributes #0`对应，并非重点内容。函数定义的方式与高级语言接近，函数名命名规则与全局变量类似以`@`开始。`return 0`对应的指令为`ret i32 0`。

如果函数定义中有参数，情况会变得复杂。
如下含参的函数定义，不同点有两处:
一是函数参数的定义，这与高级语言类似;
二是一般情况下**在函数开始时，也就是函数入口，需要先取参数，即将参数加载一次**。

-------------------

#### 函数调用[call-instruction](https://llvm.org/docs/LangRef.html#call-instruction)

如上代码，调用`putchar()`对应的代码为`%13 = call i32 @putchar(i32 %12)`。
对应的指令为`call 返回类型 @id(参数)`。

#### 习题一

上述例子中,`a=a+'0'`对应的关键代码为`%10 = add nsw i32 %9, 48`。
其中，'0'被转换为了对应的十进制数字48。
请阅读并修改下面的IR代码（`task401/task1.ll`），使之能够正确输出`HUSTCSE`。

```python
; ModuleID = 'task1.c'
source_filename = "task1.c"
target datalayout = "e-m:e-p270:32:32-p271:32:32-p272:64:64-i64:64-f80:128-n8:16:32:64-S128"
target triple = "x86_64-pc-linux-gnu"

; Function Attrs: noinline nounwind optnone uwtable
define dso_local i32 @main() #0 {
  %1 = alloca i32, align 4   ;a
  %2 = alloca i32, align 4	  ;b
  store i32 0, i32* %1, align 4   ;
  store i32 1, i32* %2, align 4
  store i32 72, i32* %2, align 4
  %3 = load i32, i32* %2, align 4
  %4 = call i32 @putchar(i32 %3)
  ;********begin********

  ;********end********
  store i32 10, i32* %2, align 4
  %aaa = load i32, i32* %2, align 4
  %bbb = call i32 @putchar(i32 %aaa)
  ret i32 0
}

declare dso_local i32 @putchar(i32) #1

attributes #0 = { noinline nounwind optnone uwtable "correctly-rounded-divide-sqrt-fp-math"="false" "disable-tail-calls"="false" "frame-pointer"="all" "less-precise-fpmad"="false" "min-legal-vector-width"="0" "no-infs-fp-math"="false" "no-jump-tables"="false" "no-nans-fp-math"="false" "no-signed-zeros-fp-math"="false" "no-trapping-math"="false" "stack-protector-buffer-size"="8" "target-cpu"="x86-64" "target-features"="+cx8,+fxsr,+mmx,+sse,+sse2,+x87" "unsafe-fp-math"="false" "use-soft-float"="false" }
attributes #1 = { "correctly-rounded-divide-sqrt-fp-math"="false" "disable-tail-calls"="false" "frame-pointer"="all" "less-precise-fpmad"="false" "no-infs-fp-math"="false" "no-nans-fp-math"="false" "no-signed-zeros-fp-math"="false" "no-trapping-math"="false" "stack-protector-buffer-size"="8" "target-cpu"="x86-64" "target-features"="+cx8,+fxsr,+mmx,+sse,+sse2,+x87" "unsafe-fp-math"="false" "use-soft-float"="false" }

!llvm.module.flags = !{!0}
!llvm.ident = !{!1}

!0 = !{i32 1, !"wchar_size", i32 4}
!1 = !{!"clang version 10.0.0-4ubuntu1 "}

```

#### 分支指令

为了完成Mini-C语言中的if语句，我们除了前面介绍的简单计算外，还需分支语句。C语言中，分支结构主要由`if-else`、`while`、`switch`、`function()函数调用`等完成程序控制流跳转。
在LLVM IR中，用终结指令[terminator-instructions](https://llvm.org/docs/LangRef.html#terminator-instructions)产生控制流。
这类指令包括[ret](https://llvm.org/docs/LangRef.html#ret-instruction), [br](https://llvm.org/docs/LangRef.html#br-instruction), `switch`, `indirectbr`, `invoke`等。
如前例中C片段：

```python
  if(a>0){
        a = a+'0';
    } 
```

对应控制流IR代码为：

```python
%7 = icmp sgt i32 %6, 0   
br i1 %7, label %8, label %11 
```

通过`icmp`指令先进行比较，然后根据结果，利用`br`指令进行跳转。

`icmp`与汇编中的`cmp`指令类似，只不过标志位放在了前面，且返回值为`i1`类型，`sgt`表示 signed greater than，它的标志位可参考[icmp Instruction](https://llvm.org/docs/LangRef.html#id1982)[¶](https://llvm.org/docs/LangRef.html#icmp-instruction)。

`br`指令语法包括条件转移与无条件转移两种格式。上面例子是条件转移：根据`%7`的值，为`1`则转`label %8`，为`0`则转`label %11`。无条件转移，例如`br label %8`。
`label`为一个代码位置标签。[br Instruction](https://llvm.org/docs/LangRef.html#id1649)[¶](https://llvm.org/docs/LangRef.html#br-instruction)。

Mini-C语言中的`while`循环控制，可以通过`icmp`和`br`指令表达其语义。

#### 习题二

请利用`icmp`和`br`指令，填充代码`task401/task2.ll，`实现`if-else`语句翻译，完成以下功能：

功能描述：程序根据输入的单个字符进行判断，如果是`a`，则输出`Y`；否则，输出`N`。
（其中，输入部分已经由`getchar()`完成，值保存在变量`%2`中。

```
; ModuleID = 'task2.c'
source_filename = "task2.c"
target datalayout = "e-m:e-p270:32:32-p271:32:32-p272:64:64-i64:64-f80:128-n8:16:32:64-S128"
target triple = "x86_64-pc-linux-gnu"

; Function Attrs: noinline nounwind optnone uwtable
define dso_local i32 @main() #0 {
  %1 = alloca i32, align 4
  %2 = alloca i32, align 4
  store i32 0, i32* %1, align 4
  store i32 1, i32* %2, align 4
  %3 = call i32 @getchar()
  store i32 %3, i32* %2, align 4
  ;********begin********
  
  ;******** end ********
  %aaa = call i32 @putchar(i32 10);输出换行符号
  store i32 0, i32* %2, align 4
  ret i32 0
}

declare dso_local i32 @getchar() #1

declare dso_local i32 @putchar(i32) #1

attributes #0 = { noinline nounwind optnone uwtable "correctly-rounded-divide-sqrt-fp-math"="false" "disable-tail-calls"="false" "frame-pointer"="all" "less-precise-fpmad"="false" "min-legal-vector-width"="0" "no-infs-fp-math"="false" "no-jump-tables"="false" "no-nans-fp-math"="false" "no-signed-zeros-fp-math"="false" "no-trapping-math"="false" "stack-protector-buffer-size"="8" "target-cpu"="x86-64" "target-features"="+cx8,+fxsr,+mmx,+sse,+sse2,+x87" "unsafe-fp-math"="false" "use-soft-float"="false" }
attributes #1 = { "correctly-rounded-divide-sqrt-fp-math"="false" "disable-tail-calls"="false" "frame-pointer"="all" "less-precise-fpmad"="false" "no-infs-fp-math"="false" "no-nans-fp-math"="false" "no-signed-zeros-fp-math"="false" "no-trapping-math"="false" "stack-protector-buffer-size"="8" "target-cpu"="x86-64" "target-features"="+cx8,+fxsr,+mmx,+sse,+sse2,+x87" "unsafe-fp-math"="false" "use-soft-float"="false" }

!llvm.module.flags = !{!0}
!llvm.ident = !{!1}

!0 = !{i32 1, !"wchar_size", i32 4}
!1 = !{!"clang version 10.0.0-4ubuntu1 "}

```

### 参考资料

[LLVM* IR Turtorial (2019)](https://www.bilibili.com/video/BV1kX4y1g7CW?from=search&seid=17795714941951506448&spm_id_from=333.337.0.0)
[LLVM：The Architecture of Open Source Applications](https://www.aosabook.org/en/llvm.html)

[LLVM IR入门指南-知乎](https://www.zhihu.com/column/c_1267851596689457152)

[LLVM样例语言实现：Kaleidoscope: Implementing a Language with LLVM](https://llvm.org/docs/tutorial/index.html#kaleidoscope-implementing-a-language-with-llvm)

[LLVM官方文档：LLVM Language Reference Manual](https://llvm.org/docs/LangRef.html)
